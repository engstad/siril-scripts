requires 1.2.0

###########################################################################################
#
# HA + OIII
#
###########################################################################################

# Convert Light Frames to .fit files
cd Light/HA-OIII
convert light -out=../../process/ha-oiii
cd ../../process/ha-oiii

# Calibrate Light Frames
# calibrate light -dark=../../Masters/dark_stacked -cc=dark -cfa -equalize_cfa
calibrate light -cfa

seqextract_HaOIII pp_light -resample=ha

# Align Ha lights
register Ha_pp_light

# Stack calibrated Ha lights to Ha_stack (temporary)
stack r_Ha_pp_light rej 3 3 -norm=addscale -output_norm -32b -out=results_00001

# and flip if required
mirrorx_single results_00001

# Align OIII lights
register OIII_pp_light

# Stack calibrated OIII lights to OIII_stack (temporary)
stack r_OIII_pp_light rej 3 3 -norm=addscale -output_norm -32b -out=results_00002

# and flip if required
mirrorx_single results_00002

load results_00001
save ../hoso/hoso_00001

load results_00002
save ../hoso/hoso_00002

###########################################################################################
#
# SII + OIII
#
###########################################################################################

# Convert Light Frames to .fit files
cd ../../Light/OIII-SII
convert light -out=../../process/sii-oiii
cd ../../process/sii-oiii

# Calibrate Light Frames
# calibrate light -dark=../Masters/dark_stacked -flat=../Masters/pp_flat_stacked -cc=dark -cfa -equalize_cfa
# calibrate light -dark=../../Masters/dark_stacked -cc=dark -cfa -equalize_cfa
calibrate light

seqextract_HaOIII pp_light -resample=ha

# Align Ha lights
register Ha_pp_light

# Stack calibrated Ha lights to Ha_stack (temporary)
stack r_Ha_pp_light rej 3 3 -norm=addscale -output_norm -32b -out=results_00001

# and flip if required
mirrorx_single results_00001

# Align OIII lights
register OIII_pp_light

# Stack calibrated OIII lights to OIII_stack (temporary)
stack r_OIII_pp_light rej 3 3 -norm=addscale -output_norm -32b -out=results_00002

# and flip if required
mirrorx_single results_00002

load results_00001
save ../hoso/hoso_00003

load results_00002
save ../hoso/hoso_00004

###########################################################################################
#
# HA + OIII + SII + OIII
#
###########################################################################################

cd ../hoso

# Align the result images, small shifts and chromatic aberrations can occur
register hoso -transf=shift -interp=none

pm ($r_hoso_00002$+$r_hoso_00004$)*0.5
save r_oiii
pm $r_oiii$*mad($r_hoso_00001$)/mad($r_oiii$)-mad($r_hoso_00001$)/mad($r_oiii$)*median($r_oiii$)+median($r_hoso_00001$)
save oiii

load r_hoso_00003
pm $r_hoso_00003$*mad($r_hoso_00001$)/mad($r_hoso_00003$)-mad($r_hoso_00001$)/mad($r_hoso_00003$)*median($r_hoso_00003$)+median($r_hoso_00001$)
save sii

load r_hoso_00001
save ha

rgbcomp sii ha oiii -out=sho -nosum

load sho
save ../../result_sho_$LIVETIME:%d$s

# # Renorm OIII to Ha using PixelMath
# # O3 * mad(HA) / mad(O3) - mad(HA) / mad(O3) * median(O3) + median(HA) =
# # (O3 - median(O3)) * mad(HA) / mad(O3) + median(HA)
# pm $r_results_00002$*mad($r_results_00001$)/mad($r_results_00002$)-mad($r_results_00001$)/mad($r_results_00002$)*median($r_results_00002$)+median($r_results_00001$)
# save ../../results/result_OIII_$LIVETIME:%d$s

# # Save Ha final result
# load r_results_00001
# save ../../results/result_Ha_$LIVETIME:%d$s

# close



